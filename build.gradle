import static org.gradle.internal.os.OperatingSystem.*

buildscript {

    ext {
        kotlin = 'org.jetbrains.kotlin:kotlin'
        kotlin_version = '1.3.41'
        kotlintest_version = "3.3.3"
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "$kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
    }
}

group = 'com.github.kotlin_graphics'

ext {
    moduleName = "${group}.glm"
    unsigned_version = 'ba1b5c2ab0231aabbef753df5fa0d0de98142360'
    kool_version = 'd052c5e18a06642fcdf9f5942198ee9c4cee1a3f'
    lwjgl_version = "3.2.2"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    // jitpack
    apply plugin: 'maven'
    group = 'com.github.kotlin-graphics'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {

        implementation "org.jetbrains.kotlin:kotlin-stdlib"

        testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
//        archives javadocJar
    }

    jar {
        inputs.property("moduleName", moduleName)
//        manifest.attributes('Automatic-Module-Name': moduleName)
    }

    compileKotlin {
        // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
        kotlinOptions {
            jvmTarget = "11"
//        javaParameters = true
        }
        // As per https://stackoverflow.com/a/47669720
        // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
        destinationDir = compileJava.destinationDir
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
//        javaParameters = true
        }
    }

    compileJava {
//        dependsOn(':compileKotlin')
        doFirst {
            options.compilerArgs = ['--module-path', classpath.asPath,]
            classpath = files()
        }
    }
}

project(":glm") {

    apply plugin: 'java-library'

    dependencies {

        def kx = "com.github.kotlin-graphics"
        implementation "$kx:kotlin-unsigned:$unsigned_version"
        implementation "$kx:kool:$kool_version"

        testImplementation project(":glm-test")

        testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version"

        ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb"].each {
            implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
            implementation "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
        }
    }

    test.useJUnitPlatform()
}

project(":glm-test") {

    dependencies {
        implementation project(":glm")

        implementation "io.kotlintest:kotlintest-core:$kotlintest_version"
        implementation "io.kotlintest:kotlintest-assertions:$kotlintest_version"
    }
}

