import org.gradle.internal.os.OperatingSystem

apply plugin: 'kotlin'
//apply plugin: 'java'

// jitpack
apply plugin: 'maven'
group = 'com.github.kotlin-graphics'

apply plugin: 'com.github.johnrengelman.shadow'

buildscript {

    ext.kotlinVersion = '1.3.0-rc-152'

    repositories {
        jcenter()   // shadow
        mavenCentral()
        maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
        maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation 'com.github.kotlin-graphics:kotlin-unsigned:98b482c3dd3e54164d4f0bb81a733e1d395aff12'
    implementation 'com.github.kotlin-graphics:kool:07890af4dd4428cc389b0749b5672413e5a8040b'

    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.1.10'

    ext.lwjglVersion = "3.2.0"
    switch (OperatingSystem.current()) {
        case OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion:$lwjglNatives"
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl("https://jitpack.io") }
}

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}